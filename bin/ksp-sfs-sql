#!/usr/bin/perl

use strict;
use warnings;

use DBI;
use JSON;
use File::Temp;
use URI;
use KSP qw(:all);

##################################################

# process parameters

##################################################

our $VERSION = $KSP::VERSION;

binmode $_, ":utf8" foreach (\*STDOUT, \*STDERR);

sub help_message() {
	my $script = $0;
	$script =~ s/.*\///;
	"usage: $script [ -dip ] [ <db> ] <save file or save name>\n";
}

use Getopt::Std;
$Getopt::Std::STANDARD_HELP_VERSION = 1;

sub HELP_MESSAGE {
	my ($out, $pkg, $ver, $swtch) = @_;
	print $out help_message();
}

sub VERSION_MESSAGE {
	my ($out, $pkg, $ver, $swtch) = @_;
	print $out "Version $VERSION\n";
}

our %opt = ();
getopts("dip", \%opt) or die help_message();

our $FILE = ($opt{p} ? "persistent" : "quicksave") . ".sfs";

our $DEBUG = $opt{d};
our $INDEX = $opt{i};
our $RUNSQLITE;

our $SW = stopwatch;

our ($DBNAME, $SFSNAME);
if (@ARGV == 2) {
	($DBNAME, $SFSNAME) = @ARGV;
} elsif (@ARGV == 1) {
	$DBNAME = File::Temp->new(DIR => "/var/tmp", TEMPLATE => "ksp-sfs-XXXXXXXX", SUFFIX => ".db");
	($SFSNAME) = @ARGV;
	$RUNSQLITE = 1;
} else {
	die help_message();
}

if ($SFSNAME !~ /\// && ! -f $SFSNAME) {
	$SFSNAME = KSP::HOME() . "/saves/$SFSNAME/$FILE";
}

##################################################

# analyze savefile

##################################################

our $system = KSP::SolarSystem->load();

sub bodyName($) {
	my ($b) = @_;
	defined $b or return undef;
	$b =~ /^\d+$/ and $b = $system->body($b);
	ref $b or return undef;
	$b->name
}

sub extractPrefix($@) {
	my ($str, @re) = @_;
	foreach my $re (@re) {
		$str =~ $re and return ($1, $2);
	}
	("", $str)
}

sub parseScienceId($) {
	my ($id) = @_;
	my $tmp = $id;
	my $experiment = $tmp =~ s/^(.*?)@// ? $1 : undef;
	(my $body, $tmp) = extractPrefix($tmp, $system->bodyPrefixMatchers);
	(my $situation, $tmp) = extractPrefix($tmp, $system->situationPrefixMatchers);
	(my $biome, $tmp) = extractPrefix($tmp, $system->body($body)->biomePrefixMatchers);

	if ($biome && $tmp) {
		$biome = "$biome$tmp";
		$tmp = "";
	}

	if (!$tmp && $experiment =~ s/_(\w+)$//) {
		$tmp = $1;
	}

	$tmp =~ s/^_//;

	my @ret = map { (defined $_ && $_ eq "") ? undef : $_ } ($experiment, $body, $situation, $biome, $tmp);
	wantarray ? @ret : \@ret
}

$SW->reset->start;
printf "-- Parsing sfs $SFSNAME, %1.1fKiB\n", (-s $SFSNAME) / 1024;
our $SFS = KSP::ConfigNode->load($SFSNAME);
our $TITLE = "Untitled";
eval { $TITLE = $SFS->getnodes("GAME")->get("Title") };
printf "-- Parsed $TITLE in %1.3f seconds\n", $SW->read;

##################################################

# save to sqlite

##################################################

printf "-- Creating db $DBNAME\n";
our $DB = DBI->connect("dbi:SQLite:dbname=$DBNAME", "", "", {
	AutoCommit => 1,
	RaiseError => 1,
});

sub qi($) {
	$DB->quote_identifier($_[0])
}

sub run($) {
	my ($cmd) = @_;
	$cmd =~ s/^\s+//;
	$cmd =~ s/\s+$//;
	local $| = 1;
	print "sql> $cmd; " if $DEBUG;
	my $sw = KSP::StopWatch->new->start;
	my $ret = $DB->do($cmd);
	printf "-- %1.3fs\n", $sw->read if $DEBUG;
	$ret
}

our @Q = ();

sub enqueue($) {
	push @Q, @_;
}

sub runqueue() {
	run(shift @Q) while @Q;
}

our %insert_count = ();

sub mktable($@) {
	my $table = shift;
	my (@col, @fill);
	foreach (@_) {
		if (UNIVERSAL::isa($_, "CODE")) {
			push @fill, $_;
		} else {
			push @col, $_;
		}
	}
	my @index = ();
	$insert_count{$table} = 0;
	run("drop table if exists " . qi($table));
	run("create table " . qi($table) . " ("
		. join(",", map {
			my $col = $_;
			my $type = "text collate nocase";
			if ($col =~ s/:(\w+)$//) {
				# warn "COL\t$col\t$type";
				if ($1 eq "r") {
					$type = "real";
				} elsif ($1 eq "i") {
					$type = "integer";
				} elsif ($1 eq "b") {
					$type = "boolean";
				}
			}
			push @index, $col;
			"\n\t" . qi($col) . " " . $type
		} @col)
		. ")");

	if ($INDEX) {
		foreach my $c (@index) {
			enqueue("create index " . qi("idx_${table}_${c}") . " on " . qi($table) . " (" . qi($c). ")");
		}
	}

	$SW->reset->start;
	$DB->begin_work;
	@fill or die "OLDSTYLE \U$table\E\n";
	$_->() foreach @fill;
	$DB->commit;
	printf "-- Generated table %s [%d] in %1.3f seconds\n",
		$table, $insert_count{$table}, $SW->read;
}

our %_insert_handle = ();

sub insert($@) {
	my ($table, @val) = @_;
	@val == 1 && ref $val[0] eq "ARRAY" and @val = @{$val[0]};
	my $n = @val;
	my $h = $_insert_handle{$table}[$n] ||= do {
		my $i = "insert into " . qi($table) . " values (" . join(", ", (("?") x $n)) . ")";
		# print "-- $i\n";
		$DB->prepare_cached($i)
	};
	$insert_count{$table}++;
	$h->execute(map {
		!defined ? undef :
		/^false$/i ? 0 :
		/^true$/i ? 1 :
		$_
	} @val)
}

sub mkview($$) {
	my ($name, $select) = @_;
	my $sw = stopwatch->start;
	$DB->begin_work;
	run("drop view if exists " . qi($name));
	run("create view " . qi($name) . " as $select");
	$DB->commit;
	printf "-- Generated view %s in %1.3f seconds\n",
		$name, $SW->read;
}

sub maplink($$$) {
	my ($body, $lat, $lon) = @_;
	my $uri = URI->new("http://localhost/~pietro/ksp/ksp-sphere-map/main.html");
	$uri->query_form(
		body => lc $body,
		lat => sprintf("%1.3f", $lat),
		lon => sprintf("%1.3f", $lon));
	$uri
}

my $bodysort = 0;
mktable(bodies => qw(body parent idx:i sort:i atmosphere:b ocean:b surface:b atm_thr:r spc_thr:r), sub {
	foreach my $b ($system->bodies) {
		insert("bodies",
			$b->name,
			($b->parent ? $b->parent->name : undef),
			$b->index,
			$bodysort++,
			($b->atmosphereDepth > 0 ? 1 : 0),
			($b->hasOcean ? 1 : 0),
			($b->hasSurface ? 1 : 0),
			($b->atmosphereDepth > 0 ? 0 + $b->atmosphereThreshold : undef),
			0 + $b->spaceThreshold);
	}
});

mktable(science_values => qw(body situation value:r), sub {
	foreach my $b ($system->bodies) {
		my $s = $b->json->{science};
		foreach my $k (sort keys %$s) {
			$k =~ /^(.+)DataValue$/ or next;
			insert("science_values", $b->name, $1, $s->{$k});
		}
	}
});

mktable(biomes_raw => qw(body biome ismini:b), sub {
	foreach my $b ($system->bodies) {
		foreach my $i (undef, $b->biomes) {
			insert("biomes_raw", $b->name, $i, 0);
		}
		foreach my $i ($b->miniBiomes) {
			insert("biomes_raw", $b->name, $i, 1);
		}
	}
});

mkview(biomes => q{
	select body, biome
	from biomes_raw
	where not ismini
});

my $scienceGainMultiplier = 1;
foreach my $c ($SFS->find("CAREER")) {
	my $sm = 1 * $c->get("ScienceGainMultiplier");
	$sm > 0 && $sm < 1.00001 and $scienceGainMultiplier = $sm;
}

mktable(science_raw => qw(experiment body situation biome extra points:r full:r id), sub {
	foreach my $n ($SFS->find("Science", id => qr/\@/)) {
		my $id = $n->get("id");
		my $points = $scienceGainMultiplier * $n->get("sci");
		my $full = $scienceGainMultiplier * $n->get("cap");
		insert("science_raw", parseScienceId($id), $points, $full, $id);
	}
});

mkview(science => q{
	select bodies.body,
		situation,
		biome,
		experiment,
		extra,
		printf('%6.2f', points) as points,
		printf('%6.2f', full) as full,
		printf('%1.2f', points / full) as perc
	from science_raw, bodies
	where science_raw.body = bodies.body
	order by bodies.sort, situation, extra, experiment, extra, full
});

mkview(results => q{
	select distinct experiment, situation,
		iif(biome is null, 1, 0) as nullbiome
	from science_raw
	where experiment <> 'ROCScience'
	order by experiment, situation
});

mkview(situations => q{
	select distinct s.body, s.situation
	from science_values as s
	left join bodies as b
		on s.body = b.body
	where s.situation <> 'Recovery'
	order by b.sort, situation
});

mkview(biome_sources => q{
	select distinct experiment, situation
	from science
	where biome is not null
	order by experiment, situation
});

mkview(sources_full => q{
	select *
	from biomes
	join (select experiment, situation from results where nullbiome = 0)
	where biome is not null and body in (select distinct body from science_raw)
	union
	select *
	from biomes
	join (select experiment, situation from results where nullbiome > 0)
	where biome is null and body in (select distinct body from science_raw)
});

mkview(sources => q{
	select bodies.body, biome, experiment, situation
	from sources_full
	left join bodies
		on sources_full.body = bodies.body
	where not (situation like 'Srf%' or situation like 'Flying%')
		or (situation like 'Flying%' and atmosphere)
		or (situation = 'SrfSplashed' and ocean)
		or (situation = 'SrfLanded' and surface)
});

mkview(report => q{
	select sources.*,
		coalesce(science.points, 0.0) as points,
		science.full,
		coalesce(science.perc, 0.0) as perc
	from sources
	left join science
	on sources.body = science.body
		and (sources.biome = science.biome or (sources.biome is null and science.biome is null))
		and sources.experiment = science.experiment
		and (sources.situation = science.situation or (sources.situation is null and science.situation is null))
});

mktable(roc => qw(body name biome), sub {
	foreach my $b ($system->bodies) {
		my $i = $b->rocInfo;
		foreach my $r (keys %$i) {
			foreach my $n (@{$i->{$r}}) {
				insert("roc", $b->name, $r, $n);
			}
		}
	}
});

my @kerbal = ();
my @roster_cols = qw(name gender trait type state badS:b hero:b veteran:b tour:b inactive:b);
mktable(roster => @roster_cols, sub {
	s/:\w+$// foreach @roster_cols;
	foreach my $r ($SFS->find("ROSTER")) {
		foreach my $k ($r->getnodes("KERBAL")) {
			push @kerbal, $k;
			insert("roster", map { scalar $k->get($_) } @roster_cols);
		}
	}
});

mktable(career => qw(name flight body situation), sub {
	foreach my $k (@kerbal) {
		my $n = $k->get("name");
		my $c = $k->getnodes("CAREER_LOG");
		if ($n && $c) {
			foreach my $l ($c->values) {
				my $k = $l->name;
				$k =~ /^[0-9]+$/ or next;
				my ($s, $b) = split /\s*,\s*/, $l->value;
				insert("career", $n, $k, $b, $s);
			}
		}
	}
});

mktable(resources_raw => qw(body:i biome), sub {
	foreach my $r ($SFS->find("SCENARIO", name => "ResourceScenario")) {
		foreach my $k ($r->find(qr/_DATA$/)) {
			my $id = $k->get("PlanetId");
			defined $id or next;
			my $biome = $k->get("BiomeName");
			$biome and $biome =~ s/\s+//gs;
			insert("resources_raw", $id, $biome);
		}
	}
});

mkview(resources => q{
	select bodies.body, resources_raw.biome
	from resources_raw
	left join bodies
		on resources_raw.body = bodies.idx
	order by bodies.sort, biome
});

my @vessels_cols = qw(persistentId:i pid type name lct:r ctrl:b sit);
mktable(vessels_raw => @vessels_cols, qw(body parts:i uri), sub {
	s/:\w+$// foreach @vessels_cols;
	foreach my $r ($SFS->find("VESSEL")) {
		my $b = $r->getnodes("ORBIT");
		$b = $b->get("REF") if $b;
		$b = bodyName($b);
		my @parts = $r->getnodes("PART");
		my $parts = @parts;
		my $uri = undef;
		my $sit = $r->get("sit") || "";
		if ($sit =~ /landed|splashed|prelaunch/i) {
			my $lat = $r->get("lat");
			my $lon = $r->get("lon");
			$uri = maplink($b, $lat, $lon);
		}
		insert("vessels_raw", (map { scalar $r->get($_) } @vessels_cols), $b, $parts, $uri);
	}
});

mkview(vessels => q{
	select persistentId as id,
		sit as situation,
		vessels_raw.body,
		ctrl, parts,
		round(lct / 3600 / 6) as launch,
		type, name
	from vessels_raw, bodies
	where vessels_raw.body = bodies.body
	order by bodies.sort, lct
});

mktable(waypoints => qw(name body lat:r lon:r uri), sub {
	foreach my $r ($SFS->find("WAYPOINT")) {
		my $name = $r->get("name");
		my $body = $r->get("celestialName");
		my $lat = $r->get("latitude");
		my $lon = $r->get("longitude");
		grep { !defined } ($name, $body, $lat, $lon)
			or insert("waypoints", $name, $body, $lat, $lon, maplink($body, $lat, $lon));
	}
	foreach my $c ($SFS->find("CONTRACT")) {
		my $type = $c->get("state", "");
		$type =~ /^(offered|active)$/i or next;
		my $body = bodyName($c->get("targetBody"));
		defined $body or next;
		my $name = $c->get("locationName");
		defined $name or next;
		my @w = $c->getnodes("PARAM", name => qr/waypoint/i);
		@w or next;
		foreach my $w (@w) {
			my $lat = $w->get("wpLatitude");
			my $lon = $w->get("wpLongitude");
			defined $lat && defined $lon or next;
			insert("waypoints", $name, $body, $lat, $lon, maplink($body, $lat, $lon));
		}
	}
});

mkview(maplinks_full => q{
	select vessels_raw.body, substring(type, 1, 9) as type, name, uri
	from vessels_raw
	where uri is not null
	union
	select waypoints.body, 'Waypoint' as type, name, uri
	from waypoints
	where uri is not null
});

mkview(maplinks => q{
	select maplinks_full.*
	from maplinks_full, bodies
	where maplinks_full.body = bodies.body
	order by bodies.sort, name
});

enqueue("analyze");
enqueue("vacuum");

runqueue();

if ($RUNSQLITE) {
	my $init = File::Temp->new(DIR => "/var/tmp",
		TEMPLATE => "ksp-sfs-XXXXXXXX",
		SUFFIX => ".sqlite");
	my $prompt = $TITLE;
	$prompt =~ s/\(.*$//;
	$prompt =~ s/[^\w\-\.]//;
	print $init ".read \"$ENV{HOME}/.sqliterc\"\n"
		if -f "$ENV{HOME}/.sqliterc";
	print $init ".prompt \"$prompt> \" \"", ("." x length($prompt)), "> \"\n";
	print $init ".mode table --wrap 256 --wordwrap off --noquote\n";
	$init->flush();
	system "sqlite3", "-init", $init->filename, $DBNAME;
}

