#!/usr/bin/perl

use strict;
use warnings;

use DBI;
use JSON;
use File::Temp;
use URI;
use KSP qw(:all);

binmode \*STDOUT, ":utf8";

##################################################

# process parameters

##################################################

our $script = $0;
$script =~ s/.*\///;
our $usage = "usage: $script [ -dip ] [ <db> ] <save file or save name>\n";

use Getopt::Std;
our %opt = ();
getopts("dip", \%opt) or die $usage;

our $FILE = ($opt{p} ? "persistent" : "quicksave") . ".sfs";

our $DEBUG = $opt{d};
our $INDEX = $opt{i};
our $RUNSQLITE;

our $SW = stopwatch;

our ($DBNAME, $SFSNAME);
if (@ARGV == 2) {
	($DBNAME, $SFSNAME) = @ARGV;
} elsif (@ARGV == 1) {
	$DBNAME = File::Temp->new(DIR => "/var/tmp", TEMPLATE => "$script-XXXXXXXX", SUFFIX => ".db");
	($SFSNAME) = @ARGV;
	$RUNSQLITE = 1;
} else {
	die $usage;
}

if ($SFSNAME !~ /\// && ! -f $SFSNAME) {
	$SFSNAME = KSP::HOME() . "/saves/$SFSNAME/$FILE";
}

##################################################

# analyze savefile

##################################################

our $system = KSP::SolarSystem->load();

sub extractPrefix($@) {
	my ($str, @re) = @_;
	foreach my $re (@re) {
		$str =~ $re and return ($1, $2);
	}
	("", $str)
}

sub parseScienceId($) {
	my ($id) = @_;
	my $tmp = $id;
	my $experiment = $tmp =~ s/^(.*?)@// ? $1 : undef;
	(my $body, $tmp) = extractPrefix($tmp, $system->bodyPrefixMatchers);
	(my $situation, $tmp) = extractPrefix($tmp, $system->situationPrefixMatchers);
	(my $biome, $tmp) = extractPrefix($tmp, $system->body($body)->biomePrefixMatchers);

	if ($biome && $tmp) {
		$biome = "$biome$tmp";
		$tmp = "";
	}

	if (!$tmp && $experiment =~ s/_(\w+)$//) {
		$tmp = $1;
	}

	$tmp =~ s/^_//;

	my @ret = map { (defined $_ && $_ eq "") ? undef : $_ } ($experiment, $body, $situation, $biome, $tmp);
	wantarray ? @ret : \@ret
}

$SW->reset->start;
printf "-- Parsing sfs $SFSNAME, %1.1fKiB\n", (-s $SFSNAME) / 1024;
our $SFS = KSP::ConfigNode->load($SFSNAME);
our $TITLE = "Untitled";
eval { $TITLE = $SFS->getnodes("GAME")->get("Title") };
printf "-- Parsed $TITLE in %1.3f seconds\n", $SW->read;

##################################################

# save to sqlite

##################################################

printf "-- Creating db $DBNAME\n";
our $DB = DBI->connect("dbi:SQLite:dbname=$DBNAME", "", "", {
	AutoCommit => 1,
	RaiseError => 1,
});

sub qi($) {
	$DB->quote_identifier($_[0])
}

sub run($) {
	my ($cmd) = @_;
	$cmd =~ s/^\s+//;
	$cmd =~ s/\s+$//;
	local $| = 1;
	print "sql> $cmd; " if $DEBUG;
	my $sw = KSP::StopWatch->new->start;
	my $ret = $DB->do($cmd);
	printf "-- %1.3fs\n", $sw->read if $DEBUG;
	$ret
}

our @Q = ();

sub enqueue($) {
	push @Q, @_;
}

sub runqueue() {
	run(shift @Q) while @Q;
}

our %insert_count = ();

sub create($@) {
	my ($table, @col) = @_;
	my @idx = ();
	$insert_count{$table} = 0;
	run("drop table if exists " . qi($table));
	run("create table " . qi($table) . " ("
		. join(",", map {
			my $col = $_;
			my $type = "text collate nocase";
			if ($col =~ s/:(\w+)$//) {
				# warn "COL\t$col\t$type";
				if ($1 eq "r") {
					$type = "real";
				} elsif ($1 eq "i") {
					$type = "integer";
				} elsif ($1 eq "b") {
					$type = "boolean";
				}
			}
			push @idx, $col;
			"\n\t" . qi($col) . " " . $type
		} @col)
		. ")");
	if ($INDEX) {
		foreach my $c (@idx) {
			enqueue("create index " . qi("idx_${table}_${c}") . " on " . qi($table) . " (" . qi($c). ")");
		}
	}
}

sub view($$) {
	my ($name, $select) = @_;
	my $sw = stopwatch->start;
	$DB->begin_work;
	run("drop view if exists " . qi($name));
	run("create view " . qi($name) . " as $select");
	$DB->commit;
	printf "-- Generated view $name in %1.3f seconds\n", $SW->read;
}

our %_insert_handle = ();

sub insert($@) {
	my ($table, @val) = @_;
	@val == 1 && ref $val[0] eq "ARRAY" and @val = @{$val[0]};
	my $n = @val;
	my $h = $_insert_handle{$table}[$n] ||= do {
		my $i = "insert into " . qi($table) . " values (" . join(", ", (("?") x $n)) . ")";
		# print "-- $i\n";
		$DB->prepare_cached($i)
	};
	$insert_count{$table}++;
	$h->execute(map {
		!defined ? undef :
		/^false$/i ? 0 :
		/^true$/i ? 1 :
		$_
	} @val)
}

create(bodies => qw(index:i body sort:i parent atmosphere:b ocean:b surface:b));
$SW->reset->start;
$DB->begin_work;
my $bodysort = 0;
foreach my $b ($system->bodies) {
	insert("bodies", $b->index, $b->name, $bodysort++,
		($b->parent ? $b->parent->name : undef),
		($b->atmosphereDepth > 0 ? 1 : 0),
		($b->hasOcean ? 1 : 0),
		($b->hasSurface ? 1 : 0));
}
$DB->commit;
printf "-- Generated bodies [$insert_count{bodies}] in %1.3f seconds\n", $SW->read;

create(biomes_all => qw(body biome ismini:b));
$SW->reset->start;
$DB->begin_work;
foreach my $b ($system->bodies) {
	foreach my $i (undef, $b->biomes) {
		insert("biomes_all", $b->name, $i, 0);
	}
	foreach my $i ($b->miniBiomes) {
		insert("biomes_all", $b->name, $i, 1);
	}
}
$DB->commit;
printf "-- Generated biomes_all [$insert_count{biomes_all}] in %1.3f seconds\n", $SW->read;

view("biomes", q{
	select body, biome
	from biomes_all
	where not ismini
});

create(roc => qw(body biome name));
$SW->reset->start;
$DB->begin_work;
foreach my $b ($system->bodies) {
	my $i = $b->rocInfo;
	foreach my $r (keys %$i) {
		foreach my $n (@{$i->{$r}}) {
			insert("roc", $b->name, $n, $r);
		}
	}
}
$DB->commit;
printf "-- Generated roc [$insert_count{roc}] in %1.3f seconds\n", $SW->read;

my $scienceGainMultiplier = 1;
foreach my $c ($SFS->find("CAREER")) {
	my $sm = 1 * $c->get("ScienceGainMultiplier");
	$sm > 0 && $sm < 1.00001 and $scienceGainMultiplier = $sm;
}

create(science_full => qw(experiment body situation biome extra points:r full:r id));
$SW->reset->start;
$DB->begin_work;
foreach my $n ($SFS->find("Science", id => qr/\@/)) {
	my $id = $n->get("id");
	my $points = $scienceGainMultiplier * $n->get("sci");
	my $full = $scienceGainMultiplier * $n->get("cap");
	insert("science_full", parseScienceId($id), $points, $full, $id);
}
$DB->commit;
printf "-- Generated science_full [$insert_count{science_full}] in %1.3f seconds\n", $SW->read;

view("science", q{
	select bodies.body,
		situation,
		biome,
		experiment,
		extra,
		printf('%6.2f', points) as points,
		printf('%6.2f', full) as full,
		printf('%1.2f', points / full) as perc
	from science_full, bodies
	where science_full.body = bodies.body
	order by bodies.sort, situation, extra, experiment, extra, full
});

view("results", q{
	select distinct experiment, situation,
		iif(biome is null, 1, 0) as nullbiome
	from science_full
	where experiment <> 'ROCScience'
	order by experiment, situation
});

view("sources_full", q{
	select *
	from biomes
	join (select experiment, situation from results where nullbiome = 0)
	where biome is not null and body in (select distinct body from science_full)
	union
	select *
	from biomes
	join (select experiment, situation from results where nullbiome > 0)
	where biome is null and body in (select distinct body from science_full)
});

view("sources", q{
	select bodies.body, biome, experiment, situation
	from sources_full
	left join bodies
		on sources_full.body = bodies.body
	where not (situation like 'Srf%' or situation like 'Flying%')
		or (situation like 'Flying%' and atmosphere)
		or (situation = 'SrfSplashed' and ocean)
		or (situation = 'SrfLanded' and surface)
});

view("report", q{
	select sources.*,
		coalesce(science.points, 0.0) as points,
		science.full,
		coalesce(science.perc, 0.0) as perc
	from sources
	left join science
	on sources.body = science.body
		and (sources.biome = science.biome or (sources.biome is null and science.biome is null))
		and sources.experiment = science.experiment
		and (sources.situation = science.situation or (sources.situation is null and science.situation is null))
});

my @roster_cols = qw(name gender type trait state badS:b hero:b veteran:b tour:b inactive:b);
create(roster => @roster_cols);
create(career => qw(name flight body situation));
s/:\w+$// foreach @roster_cols;
$SW->reset->start;
$DB->begin_work;
my $career_count = 0;
foreach my $r ($SFS->find("ROSTER")) {
	foreach my $k ($r->getnodes("KERBAL")) {
		insert("roster", map { scalar $k->get($_) } @roster_cols);
		my $n = $k->get("name");
		my $c = $k->getnodes("CAREER_LOG");
		if ($n && $c) {
			foreach my $l ($c->values) {
				my $k = $l->name;
				$k =~ /^[0-9]+$/ or next;
				my ($s, $b) = split /\s*,\s*/, $l->value;
				insert("career", $n, $k, $b, $s);
			}
		}
	}
}
$DB->commit;
printf "-- Generated roster [$insert_count{roster}], career [$insert_count{career}] in %1.3f seconds\n", $SW->read;

my @resources_cols = qw(body:i biome);
create(resources_full => @resources_cols);
s/:\w+$// foreach @roster_cols;
$SW->reset->start;
$DB->begin_work;
foreach my $r ($SFS->find("SCENARIO", name => "ResourceScenario")) {
	foreach my $k ($r->find(qr/_DATA$/)) {
		my $id = $k->get("PlanetId");
		defined $id or next;
		my $biome = $k->get("BiomeName");
		$biome and $biome =~ s/\s+//gs;
		insert("resources_full", $id, $biome);
	}
}
$DB->commit;
printf "-- Generated resources_full [$insert_count{roster}], career [$insert_count{career}] in %1.3f seconds\n", $SW->read;

view("resources", q{
	select bodies.body, resources_full.biome
	from resources_full
	left join bodies
		on resources_full.body = bodies.`index`
	order by bodies.sort, biome
});

my @vessels_cols = qw(persistentId:i pid type name lct:r ctrl:b sit);
create(vessels_full => @vessels_cols, qw(body parts:i uri));
s/:\w+$// foreach @vessels_cols;
$SW->reset->start;
$DB->begin_work;
my $base_uri = URI->new("http://localhost/~pietro/ksp/ksp-sphere-map/main.html");
foreach my $r ($SFS->find("VESSEL")) {
	my $b = $r->getnodes("ORBIT");
	$b = $b->get("REF") if $b;
	$b = $system->body($b) if defined $b;
	$b = $b->name if $b;
	my @parts = $r->getnodes("PART");
	my $parts = @parts;
	my $uri = undef;
	my $sit = $r->get("sit") || "";
	my $lat = $r->get("lat");
	my $lon = $r->get("lon");
	if ($sit =~ /landed|splashed/i) {
		$uri = $base_uri;
		$uri->query_form(body => lc $b,
			lat => sprintf("%1.3f", $lat), lon => sprintf("%1.3f", $lon));
	}
	insert("vessels_full", (map { scalar $r->get($_) } @vessels_cols), $b, $parts, $uri);
}
$DB->commit;
printf "-- Generated vessels_full [$insert_count{vessels_full}] in %1.3f seconds\n", $SW->read;

view("vessels", q{
	select persistentId as id,
		sit as situation,
		body,
		ctrl, parts,
		round(lct / 3600 / 6) as launch,
		type, name
	from vessels_full
	order by lct
});

view("maplinks", q{
	select body, substring(type, 1, 9) as type, name, uri
	from vessels_full
	where uri is not null
	order by body, name
});

enqueue("analyze");
enqueue("vacuum");

runqueue();

if ($RUNSQLITE) {
	my $init = File::Temp->new(DIR => "/var/tmp",
		TEMPLATE => "$script-XXXXXXXX",
		SUFFIX => ".sqlite");
	my $prompt = $TITLE;
	$prompt =~ s/\(.*$//;
	$prompt =~ s/[^\w\-\.]//;
	print $init ".read \"$ENV{HOME}/.sqliterc\"\n"
		if -f "$ENV{HOME}/.sqliterc";
	print $init ".prompt \"$prompt> \" \"", ("." x length($prompt)), "> \"\n";
	print $init ".mode table --wrap 256 --wordwrap off --noquote\n";
	$init->flush();
	system "sqlite3", "-init", $init->filename, $DBNAME;
}

